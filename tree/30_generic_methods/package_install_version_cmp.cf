#####################################################################################
# Copyright 2013 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Package install version compare
# @description Install a package or verify if it is installed in a specific version, or higher or lower version than a version specified
#
# @parameter package_name        Name of the package to install or verify
# @parameter version_comparator  Comparator between installed version and defined version, can be ==,<=,>=,<,>,!=
# @parameter package_version     The version of the package to verify (can be "latest" for latest version)
# @parameter action              Action to perform, can be add, verify (defaults to verify)
# 
# @class_prefix package_install
# @class_parameter package_name
# This bundle will define a class package_install_${package_name}_{kept,repaired,error,ok,reached}
# 
# Usage example:
# methods:
#     "any" usebundle => package_install_version_cmp("postgresql", ">=", "9.1", "verify");

bundle agent package_install_version_cmp(package_name, version_comparator, package_version, action)
{
  vars:
      "canonified_package_name" string => canonify("${package_name}");
      "class_prefix"            string => "package_install_${canonified_package_name}";

  defaults:
      "action" string => "immediate", if_match_regex => "add";
      "action" string => "warn_only", if_match_regex => "verify|";

  classes:
      "local_package" expression => regcmp("(http:|file:)?/.*", "${package_name}");
      "version_not_specified" expression => strcmp("latest", "${package_version}");
      "immediate" expression => strcmp("immediate", "${action}");

  packages:

    ## Debian support ##
    !version_not_specified.local_package.debian::
      "${package_name}"
                        package_policy  => "addupdate",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => debian_local_install,
                        classes         => classes_generic("${class_prefix}"),
                        action          => do_if_immediate;


    !version_not_specified.!local_package.debian::
      "${package_name}"
                        package_policy  => "addupdate",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => apt_get,
                        classes         => classes_generic("${class_prefix}"),
                        action          => do_if_immediate;


    version_not_specified.local_package.debian::
      "${package_name}"
                        package_policy => "addupdate",
                        package_method => debian_local_install,
                        classes        => classes_generic("${class_prefix}"),
                        action         => do_if_immediate;

    version_not_specified.!local_package.debian::
      "${package_name}"
                        package_policy => "addupdate",
                        package_method => apt_get,
                        classes        => classes_generic("${class_prefix}"),
                        action         => do_if_immediate;

    ## RedHat support ##
    !version_not_specified.local_package.redhat::
      "${package_name}"
                        package_policy  => "addupdate",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => redhat_local_install,
                        classes         => classes_generic("${class_prefix}"),
                        action          => do_if_immediate;

    !version_not_specified.!local_package.redhat::
      "${package_name}"
                        package_policy  => "addupdate",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => yum_rpm,
                        classes         => classes_generic("${class_prefix}"),
                        action          => do_if_immediate;


    version_not_specified.local_package.redhat::
      "${package_name}"
                        package_policy => "addupdate",
                        package_method => redhat_local_install,
                        classes        => classes_generic("${class_prefix}"),
                        action         => do_if_immediate;


    version_not_specified.!local_package.redhat::
      "${package_name}"
                        package_policy => "addupdate",
                        package_method => yum_rpm,
                        classes        => classes_generic("${class_prefix}"),
                        action         => do_if_immediate;

    ## SUSE support ##
    !version_not_specified.local_package.SuSE::
      "${package_name}"
                        package_policy  => "addupdate",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => generic,
                        classes         => classes_generic("${class_prefix}"),
                        action          => do_if_immediate;

    !version_not_specified.!local_package.SuSE::
      "${package_name}"
                        package_policy  => "addupdate",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => generic,
                        classes         => classes_generic("${class_prefix}"),
                        action          => do_if_immediate;


    version_not_specified.local_package.SuSE::
      "${package_name}"
                        package_policy => "addupdate",
                        package_method => generic,
                        classes        => classes_generic("${class_prefix}"),
                        action         => do_if_immediate;


    version_not_specified.!local_package.SuSE::
      "${package_name}"
                        package_policy => "addupdate",
                        package_method => generic,
                        classes        => classes_generic("${class_prefix}"),
                        action         => do_if_immediate;

  methods:
    immediate::
      "Reports" usebundle => _logger("Install package ${package_name} in version ${package_version}", "${class_prefix}");
    !immediate::
      "Reports" usebundle => _logger("Verify that package ${package_name} is installed in version ${package_version}", "${class_prefix}");
}
