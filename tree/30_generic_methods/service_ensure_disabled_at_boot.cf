#####################################################################################
# Copyright 2017 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Service ensure disabled at boot
# @description Force a service not to be enabled at boot
#
# @parameter service_name Service name (as recognized by systemd, init.d, etc...)
#
# @class_prefix service_ensure_disabled_at_boot
# @class_parameter service_name
# This bundle will define a class service_ensure_disabled_at_boot_${canonified_service_name}_{kept,repaired,not_ok,ok,reached}

bundle agent service_ensure_disabled_at_boot(service_name)
{
  vars:

    systemctl_utility_present::
      "command_to_unregister" string => "${paths.path[systemctl]} disable ${service_name}";

    !systemctl_utility_present.chkconfig_utility_present::
      "command_to_unregister" string => "${paths.path[chkconfig]} ${service_name} off";
    !systemctl_utility_present.update_rcd_utility_present::
      "command_to_unregister" string => "${paths.path[update_rc_d]} ${service_name} disable";

    !systemctl_utility_present.!chkconfig_utility_present.!update_rcd_utility_present.chitab_utility_present::
      "command_to_unregister" string => "/usr/sbin/chitab \"`lsitab -a | grep '^${service_name}:' | sed 's/\([^:]*\):\([^:]*\):[^:]*:\(.*\)/\1:\2:off:\3/'`\"";

    !systemctl_utility_present.!chkconfig_utility_present.!update_rcd_utility_present.!chitab_utility_present.svcadm_utility_present::
      "command_to_unregister" string => "${paths.path[svcadm]} disable ${service_name}";

    any::

      "canonified_service_name" string => canonify("${service_name}");
      "canonified_command_name" string => canonify("${command_to_unregister}");

      "old_class_prefix"        string => "service_ensure_disabled_at_boot_${canonified_service_name}";
      "promisers"          slist => { @{this.callers_promisers}, cf_null }, policy => "ifdefined";
      "class_prefix"      string => canonify(join("_", "promisers"));
      "args"                     slist => { "${service_name}" };

  methods:

    !windows::
      "disable_at_boot"
        usebundle => service_check_disabled_at_boot("${service_name}");

      "undefine_at_boot"
        usebundle  => command_execution("${command_to_unregister}"),
        ifvarclass => "(!has_promiser_stack.service_check_disabled_at_boot_${canonified_service_name}_not_ok)|(has_promiser_stack.${class_prefix}_check_disabled_at_boot_not_ok)";

      "already undefined"
        usebundle  => _classes_success("${old_class_prefix}"),
        ifvarclass => "service_check_disabled_at_boot_${canonified_service_name}_ok";

      "already undefined"
        usebundle  => _classes_success("${class_prefix}"),
        ifvarclass => "${class_prefix}_check_disabled_at_boot_ok";

      "copy classes"
        usebundle  => _classes_copy("command_execution_${canonified_command_name}", "${old_class_prefix}"),
        ifvarclass => "service_check_disabled_at_boot_${canonified_service_name}_not_ok";

      "copy classes"
        usebundle  => _classes_copy("${class_prefix}_disabled_at_boot", "${class_prefix}"),
        ifvarclass => "${class_prefix}_check_disabled_at_boot_not_ok";

   any::
      "report"
        usebundle => _log("Ensure service ${service_name} is disabled at boot", "${old_class_prefix}", "${class_prefix}", @{args}),
        ifvarclass => "(!has_promiser_stack.${old_class_prefix}_reached)|(has_promiser_stack.${class_prefix}_reached)";

  services:
    windows::
      "${service_name}"
        service_policy => "stop",
        service_method => bootstart,
        classes        => classes_generic_two("${old_class_prefix}", "${class_prefix}");
}
